config {
  schema: `${constants.GA4_PROCESSED_DATA_DATASCHEMA}`,
  type: "incremental",
  bigquery: {
    partitionBy: 'date'
  },
  tags: ['items'],
}

pre_operations {
    ${ when(incremental(), `DELETE FROM ${self()} WHERE date >= DATE_SUB(CURRENT_DATE('${constants.TIME_ZONE}'), INTERVAL ${constants.REFRESH_LOOKBACK_WINDOW} DAY);`)}
}

SELECT
  ${helpers.getDate()},
  ${helpers.getEventDatetime()},
  TIMESTAMP_MICROS(event_timestamp) event_timestamp_utc,
  FARM_FINGERPRINT(
  CONCAT(
    s.session_id,
    event_timestamp,
    event_name
  )) event_id,
  event_name,
  ${helpers.getEventParam('currency', 'string', true)},
  ${helpers.getEventParam('page_location', 'string', true)},
  SPLIT(${helpers.getEventParam('page_location', 'string', false)}, "?")[SAFE_OFFSET(0)] page_location_without_query_string,
  STRUCT(e.ecommerce.transaction_id) ecommerce,
  ${when(constants.EVENT_PARAMS.length,
  helpers.getEventParams(constants.EVENT_PARAMS) + ',')}
  s.* EXCEPT(date),
  STRUCT (
    i.item_id,
    i.item_name,
    i.item_brand,
    i.item_variant,
    i.item_category,
    i.item_category2,
    i.item_category3,
    i.item_category4,
    i.item_category5,
    i.price_in_usd,
    i.price,
    i.quantity,
    i.item_revenue_in_usd,
    i.item_revenue,
    i.item_refund_in_usd,
    i.item_refund,
    i.coupon,
    i.affiliation,
    i.location_id,
    i.item_list_id,
    i.item_list_name,
    i.item_list_index,
    i.promotion_id,
    i.promotion_name,
    i.creative_name,
    i.creative_slot
  ) AS items
FROM
  `${dataform.projectConfig.defaultDatabase}.${constants.GA4_DATASCHEMA}.${constants.GA4_TABLE_PREFIX || "events_*"}` AS e
LEFT JOIN
  ${ref('sessions')} AS s ON ${helpers.getSessionId(false, "e")} = s.session_id
  AND PARSE_DATE('%Y%m%d', e.event_date) = s.date
CROSS JOIN UNNEST(items) AS i
WHERE
  ${ when(incremental(),
  `_TABLE_SUFFIX >= FORMAT_DATE("%Y%m%d", DATE_SUB(CURRENT_DATE('${constants.TIME_ZONE}'), INTERVAL ${constants.REFRESH_LOOKBACK_WINDOW} DAY)) AND`,
  when(constants.START_DATE, `_TABLE_SUFFIX >= FORMAT_DATE("%Y%m%d", DATE ${constants.START_DATE}) AND`)
  )}
  _TABLE_SUFFIX NOT LIKE "intraday%"
AND event_name IN (
  "add_payment_info",
  "add_shipping_info",
  "add_to_cart",
  "add_to_wishlist",
  "begin_checkout",
  "purchase",
  "refund",
  "remove_from_cart",
  "select_item",
  "select_promotion",
  "view_cart",
  "view_item",
  "view_item_list",
  "view_promotion"
)

